methode tr
  rechtsDrehen
*methode
methode tl
  linksDrehen
*methode
methode t2
  rechtsDrehen
  rechtsDrehen
*methode

methode f
  schritt
*methode
methode r
  tr;f;tl
*methode
methode l
  tl;f;tr
*methode
methode b
  t2;f;t2
*methode

methode draw0
  markeSetzen;f
  markeSetzen;f
  markeSetzen;f
  markeSetzen;f
  markeSetzen;tr;f
  markeSetzen;f
  markeSetzen;tr;f
  markeSetzen;f
  markeSetzen;f
  markeSetzen;f
  markeSetzen;tr;f
  markeSetzen
*methode

methode draw1
  markeSetzen;f
  markeSetzen;f
  markeSetzen;f
  markeSetzen;f
  markeSetzen
*methode

methode draw2
  markeSetzen;tr;f
  markeSetzen;f
  markeSetzen;tl;f;tl;f;f
  markeSetzen;tr;f
  markeSetzen;tr;f
  markeSetzen;f
  markeSetzen;tl;f
  markeSetzen;f
  markeSetzen;tl;f
  markeSetzen;f
  markeSetzen
*methode

methode draw3
  draw2
  tl;f
  markeSetzen
  tl;f;f
  markeLöschen
  tr;f
  markeLöschen
*methode

methode draw4
  markeSetzen;f
  markeSetzen;f
  markeSetzen;f
  markeSetzen;f
  markeSetzen;t2;f;f;tl;f
  markeSetzen;f
  markeSetzen;tr;f
  markeSetzen;f
  markeSetzen
*methode

methode draw5
  markeSetzen;tr;f
  markeSetzen;f
  markeSetzen;tl;f
  markeSetzen;f
  markeSetzen;tl;f
  markeSetzen;f
  markeSetzen;tr;f
  markeSetzen;f
  markeSetzen;tr;f
  markeSetzen;f
  markeSetzen
*methode

methode draw7
  markeSetzen;tr;f
  markeSetzen;f
  markeSetzen;t2;f;f;tr;f
  markeSetzen;f
  markeSetzen;f
  markeSetzen;f
  markeSetzen
*methode

methode draw8
  draw0
  tr;schritt(2)
  markeSetzen
*methode

methode draw6
  draw8
  tr;f;tr;f
  markeSetzen
*methode

methode draw9
  draw5
  tr
  f;f;f
  tr
  f;f
  markeSetzen
*methode

methode zuStart
  wiederhole solange nicht istNorden
    tr
  *wiederhole
  wiederhole solange nicht istWand
    f
  *wiederhole
  tr
  wiederhole solange nicht istWand
    f
  *wiederhole
  tr
*methode

methode zuSummand2
  zuStart
  f
*methode

methode zuDezimal1
  zuStart
  tr
  schritt(12)
  tl
*methode

methode zuDezimal2
  zuDezimal1
  schritt(6)
*methode

methode zuDezimal3
  zuDezimal1
  schritt(12)
*methode

methode zuNächsteDezimal3
  zuDezimal3
  tr
  wiederhole solange istMarke
    schritt(4)
  *wiederhole
  tl
*methode

methode leere
  tr
  wiederhole 10 mal
    markeLöschen
    f
  *wiederhole
  t2
  schritt(10)
  tr
*methode

methode leereDezimal3
  zuDezimal3;l
  wiederhole 3 mal
    r
    wiederhole 3 mal
      markeLöschen;f
      markeLöschen;f
      markeLöschen;f
      markeLöschen;f
      markeLöschen
      t2;schritt(4);tl;f;tl
    *wiederhole
  *wiederhole
*methode

methode leereBlöcke
  zuSummand2
  f;f
  wiederhole 3 mal
    wiederhole solange istZiegel
      aufheben
    *wiederhole
    r
  *wiederhole
*methode

methode überlauf1
  r;markeSetzen;l
*methode
methode überlauf0
  r;markeLöschen;l
*methode
methode ergebnis1
  f;markeSetzen;b
*methode
methode ergebnis0
  f;markeLöschen;b
*methode

methode additionSchritt
  wenn istMarke dann
    f
    wenn istMarke dann
      f
      wenn istMarke dann
        überlauf1
        ergebnis1
      sonst
        überlauf1
        ergebnis0
      *wenn
    sonst
      f
      wenn istMarke dann
        überlauf1
        ergebnis0
      sonst
        überlauf0
        ergebnis1
      *wenn
    *wenn
  sonst
    f
    wenn istMarke dann
      f
      wenn istMarke dann
        überlauf1
        ergebnis0
      sonst
        überlauf0
        ergebnis1
      *wenn
    sonst
      f
      wenn istMarke dann
        überlauf0
        ergebnis1
      sonst
        überlauf0
        ergebnis0
      *wenn
    *wenn
  *wenn
*methode

methode addieren
  zuStart
  wiederhole 10 mal
    additionSchritt
    tr;f;tr
    f;f;t2
  *wiederhole
*methode

methode ergebnisZuBlöcken
  zuSummand2
  f;f
  tr
  wiederhole solange istWesten
    wiederhole 10 mal
      wenn istWesten dann
        wenn istMarke dann
          markeLöschen
          t2
          wiederhole solange nicht istWand
            f
            markeSetzen
          *wiederhole
        sonst
          f
        *wenn
      *wenn
    *wiederhole
    wenn istOsten dann
      tr
      wenn istZiegel(9) dann
        aufheben(9)
        r
        wenn istZiegel(9) dann
          aufheben(9)
          r
          hinlegen(rot)
          l
        sonst
          hinlegen(gelb)
        *wenn
        l
      sonst
        hinlegen(grün)
      *wenn
      tr
    sonst
      tr
    *wenn
  *wiederhole
*methode

methode blöckeZuDezimal
  wenn nicht istZiegel dann
    zuNächsteDezimal3
    draw0
  sonst wenn istZiegel(1) dann
    zuNächsteDezimal3
    draw1
  sonst wenn istZiegel(2) dann
    zuNächsteDezimal3
    draw2
  sonst wenn istZiegel(3) dann
    zuNächsteDezimal3
    draw3
  sonst wenn istZiegel(4) dann
    zuNächsteDezimal3
    draw4
  sonst wenn istZiegel(5) dann
    zuNächsteDezimal3
    draw5
  sonst wenn istZiegel(6) dann
    zuNächsteDezimal3
    draw6
  sonst wenn istZiegel(7) dann
    zuNächsteDezimal3
    draw7
  sonst wenn istZiegel(8) dann
    zuNächsteDezimal3
    draw8
  sonst wenn istZiegel(9) dann
    zuNächsteDezimal3
    draw9
  *wenn
  *wenn
  *wenn
  *wenn
  *wenn
  *wenn
  *wenn
  *wenn
  *wenn
  *wenn
*methode

methode ergebnisZuDezimal
  ergebnisZuBlöcken
  zuSummand2;f;f
  blöckeZuDezimal
  zuSummand2;f;f
  wiederhole solange istZiegel
    aufheben
  *wiederhole
  r
  blöckeZuDezimal
  zuSummand2;f;f
  r
  wiederhole solange istZiegel
    aufheben
  *wiederhole
  r
  blöckeZuDezimal
  zuSummand2;f;f
  r;r
  wiederhole solange istZiegel
    aufheben
  *wiederhole
*methode

methode ergebnisNachSummand1
  zuStart
  wiederhole 10 mal
    f;f;f
    wenn istMarke dann
      t2;f;f;f
      markeSetzen
    sonst
      t2;f;f;f
      markeLöschen
    *wenn
    tl;f;tl
  *wiederhole
*methode

methode dezimalNachSummand2
  f;f;tr;f
  wenn nicht istMarke dann {0,1,7}
    f
    wenn istMarke dann {0}
      zuSummand2;leere;tr
    sonst
      tr;f;f
      wenn istMarke dann {7}
        zuSummand2
        leere
        tr
        markeSetzen;f
        markeSetzen;f
        markeSetzen
      sonst {1}
        zuSummand2;leere
        markeSetzen
      *wenn
    *wenn
  sonst {2,3,4,5,6,8,9}
    f
    wenn nicht istMarke dann {3}
      zuSummand2;leere;tr
      markeSetzen;f
      markeSetzen
    sonst {2,4,5,6,8,9}
      tl;f
      wenn istMarke dann {2,6,8}
        t2;f;f
        wenn istMarke dann {6,8}
          tr;f;f
          wenn istMarke dann {8}
            zuSummand2;leere;tr
            f;f;f
            markeSetzen
          sonst {6}
            zuSummand2;leere;tr
            f
            markeSetzen;f
            markeSetzen
          *wenn
        sonst {2}
          zuSummand2;leere;tr
          f;markeSetzen
        *wenn
      sonst {4,5,9}
        f
        wenn istMarke dann {5,9}
          t2;f;f;f;tr;f;f
          wenn istMarke dann {9}
            zuSummand2;leere;tr
            markeSetzen
            f;f;f
            markeSetzen
          sonst {5}
            zuSummand2;leere;tr
            markeSetzen
            f;f
            markeSetzen
          *wenn
        sonst {4}
          zuSummand2;leere;tr
          f;f
          markeSetzen
        *wenn
      *wenn
    *wenn
  *wenn
*methode

leereDezimal3
leereBlöcke
zuStart
leere

zuDezimal1
dezimalNachSummand2
addieren
ergebnisNachSummand1

zuDezimal1
tr;schritt(4);tl
dezimalNachSummand2
wiederhole 10 mal
  addieren
  ergebnisNachSummand1
*wiederhole

zuDezimal2
dezimalNachSummand2
addieren
ergebnisNachSummand1

zuDezimal2
tr;schritt(4);tl
dezimalNachSummand2
wiederhole 10 mal
  addieren
  ergebnisNachSummand1
*wiederhole

ergebnisZuDezimal